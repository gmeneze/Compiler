1. <program> --> <type name> <program left factor three>
               | empty

2. <program left factor one> --> <func> <program left factor two>
                               | empty  

3. <program left factor two> --> empty
                               | <func list>

4. <program left factor three> --> ID <program left factor four>
 
5. <program left factor four> --> <id left factor> <id list prime> semicolon <data decls left factor> <program left factor one>
                                | <func decl left factor two> <func left factor one> <program left factor two> 

6. <func list> --> <func> <func list left factor>

7. <func list left factor> --> empty
                             | <func list>

8. <func> --> <func decl> <func left factor one> 

9. <func left factor> --> semicolon
                        | left_brace <func left factor two>

10. <func left factor two> --> <data decls> <func left factor three> 

11. <func left factor three> --> right_brace
                               | <statements> right_brace 

12. <func decl> --> <type name> <func decl left factor one>

13. <func decl left factor one> --> ID <func decl left factor two> 

14. <func decl left factor two> --> left_parenthesis <func decl left factor three>

15. <func decl left factor three> --> right_parenthesis
                                    | void <func decl left factor four>
                                    | int ID <non-empty list prime> right_parenthesis
                                    | binary ID <non-empty list prime> right_parenthesis
                                    | decimal ID <non-empty list prime> right_parenthesis

16. <func decl left factor four> --> right_parenthesis
                                   | ID <non-empty list prime> right_parenthesis

17. <type name> --> int 
                  | void 
                  | binary 
                  | decimal 

18. <non-empty list> --> <type name> ID <non-empty list prime>

19. <non-empty list prime> --> comma <type name> ID <non-empty list prime>
                             | empty

20. <data decls> --> <type name> <id list> semicolon <data decls left factor>

21. <data decls left factor> --> <data decls>
                               | empty

22. <id list> --> <id> <id list prime>

23. <id list prime> --> comma <id list prime>
                      | empty

24. <id> --> ID <id left factor>

25. <id left factor> --> left_bracket <expression> right_bracket
                       | empty

26. <block statements> --> left_brace <block statements left factor> 

27. <block statements left factor> --> right_brace
                                     | <statements> right_brace

28. <statements> --> <statement> <statements left factor>

29. <statements left factor> --> <statements> 
                               | empty 

30. <statement> --> ID <statement left factor>
              | <if statement> 
              | <while statement> 
              | <return statement> 
              | <break statement> 
              | <continue statement> 
              | read left_parenthesis  ID right_parenthesis semicolon 
              | write left_parenthesis <expression> right_parenthesis semicolon 
              | print left_parenthesis  STRING right_parenthesis semicolon 

31. <statement left factor> --> <assignment left factor>
                              | <func call left factor one> 

32. <assignment> --> ID <assignment left factor>

33. <assignment left factor> --> equal_sign <expression> semicolon 
                               | left_bracket <expression> right_bracket equal_sign <expression> semicolon

34. <func call> --> ID <func call left factor one>   

35. <func call left factor one> --> left_parenthesis <func call left factor two>

36. <func call left factor two> --> right_parenthesis semicolon
                                  | <non-empty expr list> right_parenthesis semicolon

37. <non-empty expr list> --> <expression> <non-empty expr list prime>

38. <non-empty expr list prime> --> comma <expression> <non-empty expr list prime>
                                  | empty
 
39. <if statement> --> if left_parenthesis <condition expression> right_parenthesis <block statements> 

40. <condition expression> -->  <condition> <condition expression left factor>

41. <condition expression left factor> --> <condition op> <condition>
                                         | empty

42. <condition op> --> double_and_sign 
                     | double_or_sign

43. <condition> --> <expression> <comparison op> <expression> 

44. <comparison op> --> == 
                      | != 
                      | > 
                      | >= 
                      | < 
                      | <=

45. <while statement> --> while left_parenthesis <condition expression> right_parenthesis <block statements> 

46. <return statement> --> return <return statement left factor> 

47. <return statement left factor> --> <expression> semicolon 
                                 | semicolon

48. <break statement> ---> break semicolon 

49. <continue statement> ---> continue semicolon

50. <expression> --> <term> <expression prime>

51. <expression prime> --> <addop> <term> <expression prime>
               |  empty 

52. <addop> --> plus_sign 
          | minus_sign

53. <term> --> <factor> <term prime>

54. <term prime> --> <mulop> <factor> <term prime>
               | empty

55. <mulop> --> star_sign 
          | forward_slash 

56. <factor> --> ID <factor left factor one>
           | NUMBER 
           | minus_sign NUMBER 
           | left_parenthesis <expression> right_parenthesis

57. <factor left factor one> --> empty
                       | left_bracket <expression> right_bracket
                       | left_parenthesis <factor left factor two>

58. <factor left factor two> --> right_parenthesis
                               | <non-empty expr list> right_parenthesis
